/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Config file for defining DSL2 per module options and publishing paths
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Available keys to override module options:
        ext.args   = Additional arguments appended to command in module.
        ext.args2  = Second set of arguments appended to command in module (multi-tool modules).
        ext.args3  = Third set of arguments appended to command in module (multi-tool modules).
        ext.prefix = File name prefix for output files.
----------------------------------------------------------------------------------------
*/

process {

    publishDir = [
        path: { "${params.outdir}/${task.process.tokenize(':')[-1].tokenize('_')[0].toLowerCase()}" },
        mode: params.publish_dir_mode,
        saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
    ]

    //
    // CONVERT INPUT BAM
    //
    withName: SAMTOOLS_CONVERT_BAM_INPUT {
        tag = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        publishDir = [
            enabled: false
        ]
    }

    withName: CAT_FASTQ_CONVERTED_BAM {
        tag = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        publishDir = [
            enabled: false
        ]
    }

    //
    // READ PREPROCESSING
    //
    withName: FASTQC {
        tag = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        ext.args = '--quiet'
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        publishDir = [
            path: { "${params.outdir}/preprocessing/fastqc_raw/" },
            mode: params.publish_dir_mode,
        ]
    }

    withName: FASTQC_PROCESSED {
        tag = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        ext.args = '--quiet'
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        publishDir = [
            path: { "${params.outdir}/preprocessing/fastqc_processed/" },
            mode: params.publish_dir_mode,
        ]
    }

    withName: 'MULTIQC' {
        ext.args   = { params.multiqc_title ? "--title \"$params.multiqc_title\"" : '' }
        publishDir = [
            path: { "${params.outdir}/multiqc" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: FALCO {
        tag = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        ext.args = '--quiet'
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        publishDir = [
            path: { "${params.outdir}/preprocessing/falco_raw/" },
            mode: params.publish_dir_mode,
        ]
    }

    withName: FALCO_PROCESSED {
        tag = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        ext.args = '--quiet'
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        publishDir = [
            path: { "${params.outdir}/preprocessing/falco_processed/" },
            mode: params.publish_dir_mode,
        ]
    }

    withName: FASTP_SINGLE {
        tag = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        ext.args   = [
            // TRIMMING OPTIONS
            params.preprocessing_trim5p != 0 ? "--trim_front1 ${params.preprocessing_trim5p}" : "",
            params.preprocessing_trim3p != 0 ? "--trim_tail1 ${params.preprocessing_trim3p}" : "",
            params.preprocessing_skipadaptertrim ? "--disable_adapter_trimming" : params.preprocessing_adapterlist ? "" : params.preprocessing_adapter1 ? "--adapter_sequence ${params.preprocessing_adapter1}" : "",  // adding adapter list happens at module input channel level
            // FILTERING OPTIONS
            "--length_required ${params.preprocessing_minlength}",
            params.preprocessing_fastp_complexityfilter ? "--low_complexity_filter --complexity_threshold ${params.preprocessing_fastp_complexityfilter_threshold}" : ""
        ].join(' ').trim()
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        publishDir = [
            [
                path: { "${params.outdir}/preprocessing" },
                mode: params.publish_dir_mode,
                pattern: '*.fastq.gz',
                enabled: params.preprocessing_savepreprocessedreads
            ],
            [
                path: { "${params.outdir}/preprocessing" },
                mode: params.publish_dir_mode,
                pattern: '*.{log,html,json}'
            ]
        ]
    }

    withName: FASTP_PAIRED {
        tag = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        ext.args   = [
            // COLLAPSING OPTIONS - option to retain singletons
            params.preprocessing_excludeunmerged ? "" : "--include_unmerged",
            // TRIMMING OPTIONS
            params.preprocessing_trim5p != 0 ? "--trim_front1 ${params.preprocessing_trim5p} --trim_front2 ${params.preprocessing_trim5p}" : "",
            params.preprocessing_trim3p != 0 ? "--trim_tail1 ${params.preprocessing_trim3p} --trim_tail2 ${params.preprocessing_trim3p}" : "",
            params.preprocessing_skipadaptertrim ? "--disable_adapter_trimming" : params.preprocessing_adapterlist ? "" : params.preprocessing_adapter1 ? "--adapter_sequence ${params.preprocessing_adapter1}" : "",  // adding adapter list happens at module input channel level
            params.preprocessing_skipadaptertrim ? "" : params.preprocessing_adapterlist ? "" : params.preprocessing_adapter2 ? "--adapter_sequence_r2 ${params.preprocessing_adapter2}" : "",  // adding adapter list happens at module input channel level
            // FILTERING OPTIONS--disable_adapter_trimming
            "--length_required ${params.preprocessing_minlength}",
            params.preprocessing_fastp_complexityfilter ? "--low_complexity_filter --complexity_threshold ${params.preprocessing_fastp_complexityfilter_threshold}" : ""
        ].join(' ').trim()
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        publishDir = [
            [
                path: { "${params.outdir}/preprocessing" },
                mode: params.publish_dir_mode,
                pattern: '*.fastq.gz',
                enabled: params.preprocessing_savepreprocessedreads
            ],
            [
                path: { "${params.outdir}/preprocessing" },
                mode: params.publish_dir_mode,
                pattern: '*.{log,html,json}'
            ]
        ]
    }

    withName: ADAPTERREMOVAL_SINGLE {
        tag = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        ext.args   = [
            // trimming options - note: adding adapter list happens at module input channel level
            params.preprocessing_skipadaptertrim ? "--adapter1 ''" : params.preprocessing_adapterlist ? "" : params.preprocessing_adapter1 ? "--adapter1 ${params.preprocessing_adapter1}" : "", // adding adapter list happens at module input channel level
            params.preprocessing_skipadaptertrim ? "--adapter2 ''" : params.preprocessing_adapterlist ? "" : params.preprocessing_adapter2 ? "--adapter2 ${params.preprocessing_adapter2}" : "",
            "--minadapteroverlap ${params.preprocessing_adapterremoval_adapteroverlap}",
            params.preprocessing_adapterremoval_preserve5p ? "--preserve5p" : "",
            params.preprocessing_trim5p != 0 ? "--trim5p ${params.preprocessing_trim5p}" : "",
            params.preprocessing_trim3p != 0 ? "--trim3p ${params.preprocessing_trim3p}" : "",
            !params.preprocessing_adapterremoval_skipqualitytrimming ? "--trimqualities --minquality ${params.preprocessing_adapterremoval_trimbasequalitymin}" : "",
            !params.preprocessing_adapterremoval_skipntrimming ? "--trimns" : "",
            "--qualitymax ${params.preprocessing_adapterremoval_qualitymax}",
            // filtering options
            "--minlength ${params.preprocessing_minlength}"
        ].join(' ').trim()
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        publishDir = [
            [
                path: { "${params.outdir}/preprocessing" },
                mode: params.publish_dir_mode,
                pattern: '*.fastq.gz',
                enabled: params.preprocessing_savepreprocessedreads
            ],
            [
                path: { "${params.outdir}/preprocessing" },
                mode: params.publish_dir_mode,
                pattern: '*.settings'
            ]
        ]
    }

    withName: ADAPTERREMOVAL_PAIRED {
        tag = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        ext.args   = [
            // COLLAPSING OPTIONS
            params.preprocessing_skippairmerging ? "" : "--collapse",
            // TRIMMING OPTIONS  - note: adding adapter list happens at module input channel level
            params.preprocessing_skipadaptertrim ? "--adapter1 ''" : params.preprocessing_adapterlist ? "" : params.preprocessing_adapter1 ? "--adapter1 ${params.preprocessing_adapter1}" : "",
            params.preprocessing_skipadaptertrim ? "--adapter2 ''" : params.preprocessing_adapterlist ? "" : params.preprocessing_adapter2 ? "--adapter2 ${params.preprocessing_adapter2}" : "",
            "--minadapteroverlap ${params.preprocessing_adapterremoval_adapteroverlap}",
            params.preprocessing_adapterremoval_preserve5p ? "--preserve5p" : "",
            params.preprocessing_trim5p != 0 ? "--trim5p ${params.preprocessing_trim5p}" : "",
            params.preprocessing_trim3p != 0 ? "--trim3p ${params.preprocessing_trim3p}" : "",
            !params.preprocessing_adapterremoval_skipqualitytrimming ? "--trimqualities --minquality ${params.preprocessing_adapterremoval_trimbasequalitymin}" : "",
            !params.preprocessing_adapterremoval_skipntrimming ? "--trimns" : "",
            "--qualitymax ${params.preprocessing_adapterremoval_qualitymax}",
            // FILTERING OPTIONS
            "--minlength ${params.preprocessing_minlength}"
        ].join(' ').trim()
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        publishDir = [
            [
                path: { "${params.outdir}/preprocessing" },
                mode: params.publish_dir_mode,
                pattern: '*.fastq.gz',
                enabled: params.preprocessing_savepreprocessedreads
            ],
            [
                path: { "${params.outdir}/preprocessing" },
                mode: params.publish_dir_mode,
                pattern: '*.settings'
            ]
        ]
    }

    withName: CAT_FASTQ_ADAPTERREMOVAL {
        tag = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}_${meta.reference}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}_${meta.reference}" }
        publishDir = [
                path: { "${params.outdir}/preprocessing" },
                mode: params.publish_dir_mode,
                pattern: '*.fastq.gz',
                enabled: params.preprocessing_savepreprocessedreads
            ]
    }


    //
    // REFERENCE PREPARATION
    //
    withName: GUNZIP_FASTA {
        publishDir = [
            path: { "${params.outdir}/reference/${meta.id}/" },
            mode: params.publish_dir_mode,
            pattern: '*.f*',
            enabled: params.save_reference
        ]
    }

    withName: 'GUNZIP_PMDFASTA|GUNZIP_PMDSNP|GUNZIP_SNPBED' {
        publishDir = [
            enabled: false
        ]
    }

    withName: SAMTOOLS_FAIDX {
        publishDir = [
            path: { "${params.outdir}/reference/${meta.id}/" },
            mode: params.publish_dir_mode,
            pattern: '*.fai',
            enabled: params.save_reference
        ]
    }

    withName: PICARD_CREATESEQUENCEDICTIONARY {
        publishDir = [
            path: { "${params.outdir}/reference/${meta.id}/" },
            mode: params.publish_dir_mode,
            pattern: '*.dict',
            enabled: params.save_reference
        ]
    }

    withName: BOWTIE2_BUILD {
        publishDir = [
            path: { "${params.outdir}/reference/${meta.id}/" },
            mode: params.publish_dir_mode,
            pattern: 'bowtie2',
            enabled: params.save_reference
        ]
    }

    withName: BWA_INDEX {
        publishDir = [
            path: { "${params.outdir}/reference/${meta.id}/" },
            mode: params.publish_dir_mode,
            pattern: 'bwa',
            enabled: params.save_reference
        ]
    }

    // Reference elongation and indexing for circular mapping
    withName: GUNZIP_ELONGATED_FASTA {
        publishDir = [
            path: { "${params.outdir}/reference/${meta.id}_${params.mapping_circularmapper_elongation_factor}/" },
            mode: params.publish_dir_mode,
            pattern: '*[0-9].f*',
            enabled: params.save_reference
        ]
    }

    withName: CIRCULARMAPPER_CIRCULARGENERATOR {
        tag = { "${meta.id}_${params.mapping_circularmapper_elongation_factor}" }
        publishDir = [
            path: { "${params.outdir}/reference/${meta.id}_${params.mapping_circularmapper_elongation_factor}/" },
            mode: params.publish_dir_mode,
            pattern: '*_*[0-9].fasta',
            enabled: params.save_reference
        ]
    }

    withName: BWA_INDEX_CIRCULARISED {
        publishDir = [
            path: { "${params.outdir}/reference/${meta.id}_${params.mapping_circularmapper_elongation_factor}/" },
            mode: params.publish_dir_mode,
            pattern: 'bwa',
            enabled: params.save_reference
        ]
    }

    //
    // BAM INPUT
    //
    withName: 'SAMTOOLS_FLAGSTATS_BAM_INPUT' {
        // TODO Once a lane-merging step is added for input BAMs, the lane should be dropped from this tag.
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.lane}_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/bam_input_stats/" },
            mode: params.publish_dir_mode,
            pattern: '*.flagstat'
        ]
    }

    withName: SAMTOOLS_INDEX_BAM_INPUT {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
    }

    //
    // BAM FILTERING
    //
    withName: 'CAT_FASTQ_UNMAPPED|CAT_FASTQ_MAPPED' {
        tag = { "${meta.sample_id}_${meta.library_id}_${meta.reference}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}" }
        publishDir = [
                path: { "${params.outdir}/preprocessing" },
                mode: params.publish_dir_mode,
                pattern: '*.fastq.gz',
                enabled: params.preprocessing_savepreprocessedreads
            ]
    }

    withName: FILTER_BAM_FRAGMENT_LENGTH {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args = "-l ${params.bamfiltering_minreadlength}"
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/bam_filtering/" },
            mode: params.publish_dir_mode,
            pattern: '*.filtered.bam',
            enabled: params.bamfiltering_savefilteredbams
        ]
    }

    withName: SAMTOOLS_FASTQ_UNMAPPED {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args = [
            '-f 4',
        ].join(' ').trim()
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}_unmapped" }
        publishDir = [
            path: { "${params.outdir}/bam_filtering/" },
            mode: params.publish_dir_mode,
            pattern: '*.fastq.gz',
            enabled: params.bamfiltering_generateunmappedfastq
        ]
    }

    withName: SAMTOOLS_VIEW_BAM_FILTERING {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args = [
            "-q ${params.bamfiltering_mappingquality}",
            params.bamfiltering_retainunmappedgenomicbam ? '' : "-F ${params.bamfilter_genomicbamfilterflag}",
        ].join(' ').trim()
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}_filtered" }
        publishDir = [
            path: { "${params.outdir}/bam_filtering/" },
            mode: params.publish_dir_mode,
            pattern: '*.bam',
            enabled: params.bamfiltering_savefilteredbams
        ]
    }

    withName: 'SAMTOOLS_LENGTH_FILTER_INDEX|SAMTOOLS_FILTER_INDEX' {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args = { params.fasta_largeref ? "-c" : "" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}_filtered" }
        publishDir = [
            path: { "${params.outdir}/bam_filtering/" },
            mode: params.publish_dir_mode,
            pattern: '*.{bai,csi}'
        ]
    }

    withName: SAMTOOLS_FASTQ_MAPPED {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args = [
            params.metagenomicscreening_input == 'all' ? '' : '-F 4',
        ].join(' ').trim()
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}_mapped" }
        publishDir = [
            path: { "${params.outdir}/bam_filtering/" },
            mode: params.publish_dir_mode,
            pattern: '*.fastq.gz',
            enabled: params.bamfiltering_generatemappedfastq
        ]
    }

    withName: SAMTOOLS_FLAGSTAT_FILTERED {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}_filtered" }
        publishDir = [
            path: { "${params.outdir}/bam_filtering/" },
            mode: params.publish_dir_mode,
            pattern: '*.flagstat'
        ]
    }

    //
    // SHARDING FASTQS
    //
    withName: SEQKIT_SPLIT2 {
        tag = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        ext.prefix = "out"
        ext.args = "-s ${params.fastq_shard_size}"
        publishDir = [
            enabled: false
        ]
    }

    //
    // READ MAPPING
    //
    withName: BWA_ALN {
        tag = { "${meta.id_index}|${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        ext.args = { "-n ${params.mapping_bwaaln_n} -k ${params.mapping_bwaaln_k} -l ${params.mapping_bwaaln_l} -o ${params.mapping_bwaaln_o}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}_${meta.id_index}" }
        publishDir = [
            enabled: false
        ]
    }

    withName: 'BWA_SAMSE|BWA_SAMPE' {
        tag = { "${meta.id_index}|${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        ext.args = {
            se_pe_string = meta.single_end ? "SE" : "PE"
            [
                "-r '@RG\\tID:ILLUMINA-${meta.sample_id}_${meta.library_id}\\tSM:${meta.sample_id}\\tLB:${meta.library_id}\\tPL:illumina\\tPU:ILLUMINA-${meta.library_id}-${meta.strandedness}_stranded-${se_pe_string}'"
            ].join(' ').trim() }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}_${meta.id_index}" }
        publishDir = [
            enabled: false
        ]
    }

    withName: ENDORSPY {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/mapstats/endorspy" },
            mode: params.publish_dir_mode,
            pattern: '*.json'
        ]
    }

    withName: ".*MAP:FASTQ_ALIGN_BWAALN:SAMTOOLS_INDEX" {
        tag = { "${meta.id_index}|${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        ext.args = { params.fasta_largeref ? "-c" : "" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}_${meta.reference}" }
        publishDir = [
            enabled: false
        ]
    }

    withName: BWA_MEM {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        ext.args = {
            se_pe_string = meta.single_end ? "SE" : "PE"
            [
                "-k ${params.mapping_bwamem_k}",
                "-r ${params.mapping_bwamem_r}",
                "-R '@RG\\tID:ILLUMINA-${meta.sample_id}_${meta.library_id}\\tSM:${meta.sample_id}\\tLB:${meta.library_id}\\tPL:illumina\\tPU:ILLUMINA-${meta.library_id}-${meta.strandedness}_stranded-${se_pe_string}'"
            ].join(' ').trim() }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}_${meta.reference}" }
        publishDir = [
            enabled: false
        ]
    }

    withName: BOWTIE2_ALIGN {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        ext.args = {
            se_pe_string = meta.single_end ? "SE" : "PE"
            [
                "-N ${params.mapping_bowtie2_n}",
                "-L ${params.mapping_bowtie2_l}",
                "--trim5 ${params.mapping_bowtie2_trim5}",
                "--trim3 ${params.mapping_bowtie2_trim3}",
                "--maxins ${params.mapping_bowtie2_maxins}",
                "--rg-id ILLUMINA-${meta.library_id}",
                "--rg SM:${meta.sample_id}",
                "--rg LB:${meta.library_id}",
                "--rg PL:illumina",
                "--rg PU:ILLUMINA-${meta.sample_id}_${meta.library_id}-${meta.strandedness}_stranded-${se_pe_string}",
                "${params.mapping_bowtie2_alignmode}" == 'local' ? "--local --${params.mapping_bowtie2_sensitivity}-local" : ( "${params.mapping_bowtie2_alignmode}" == 'end-to-end' ? "--end-to-end --${params.mapping_bowtie2_sensitivity}" : "" )
        ].join(' ').trim()}
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}_${meta.reference}" }
        publishDir = [
            enabled: false
        ]
    }

    withName: 'SAMTOOLS_INDEX_MEM|SAMTOOLS_INDEX_BT2' {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        ext.args = { params.fasta_largeref ? "-c" : "" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}_${meta.reference}" }
        publishDir = [
            enabled: false
        ]
    }

    withName: SAMTOOLS_MERGE_LANES {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}" }
        publishDir = [
            enabled: false
        ]
        ext.args = { params.run_fastq_sharding ? "-c -p" : "" }
    }

    withName: SAMTOOLS_SORT_MERGED_LANES {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}_sorted" }
        publishDir = [
            path: { "${params.outdir}/mapping/${params.mapping_tool}/" },
            mode: params.publish_dir_mode,
            pattern: '*.{bam}'
        ]
    }

    withName: SAMTOOLS_INDEX_MERGED_LANES {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args = { params.fasta_largeref ? "-c" : "" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/mapping/${params.mapping_tool}/" },
            mode: params.publish_dir_mode,
            pattern: '*.{bai,csi}'
        ]
    }

    withName: SAMTOOLS_FLAGSTAT_MAPPED {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/mapping/${params.mapping_tool}/" },
            mode: params.publish_dir_mode,
            pattern: '*.flagstat'
        ]
    }

    // Circular mapping
    // Configuration for BWA_ALN and BWA_SAMSE/SAMPE is the same as for the non-circular mapping
    withName: CIRCULARMAPPER_REALIGNSAMFILE {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}" }
        publishDir = [
            enabled: false
        ]
    }

    withName: ".*MAP:FASTQ_ALIGN_BWAALN_ELONGATED:SAMTOOLS_INDEX" {
        tag = { "${meta.id_index}|${meta.sample_id}_${meta.library_id}_L${meta.lane}" }
        ext.args = { params.fasta_largeref ? "-c" : "" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_L${meta.lane}_${meta.reference}" }
        publishDir = [
            enabled: false
        ]
    }

    //
    // DEDUPLICATION
    //
    withName: PICARD_MARKDUPLICATES {
        tag = { "${meta.reference}:${meta.genomic_region}|${meta.sample_id}_${meta.library_id}" }
        ext.args = [
            "--REMOVE_DUPLICATES",
            "--VALIDATION_STRINGENCY SILENT",
            "--ASSUME_SORTED"
        ].join(' ').trim()
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}_${meta.genomic_region}_MarkDuplicates" }
        publishDir = [
            enabled: false
        ]
    }

    withName: DEDUP {
        tag = { "${meta.reference}:${meta.genomic_region}|${meta.sample_id}_${meta.library_id}" }
        ext.args = "--merged"
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}_${meta.genomic_region}_DeDup" }
        publishDir = [
            enabled: false
        ]
    }

    withName: SAMTOOLS_MERGE_DEDUPPED {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}" }
        publishDir = [
            enabled: false
        ]
    }

        withName: ".*DEDUPLICATE:BUILD_INTERVALS" {
        publishDir = [
            enabled: false
        ]
    }

    // Overwrite default SWF tag and prefix
    withName: ".*BAM_SPLIT_BY_REGION:SAMTOOLS_VIEW" {
        tag = { "${meta.reference}:${meta.genomic_region}|${meta.sample_id}_${meta.library_id}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}_${meta.genomic_region}_dedupped" }
        publishDir = [
            enabled: false
        ]
    }

    withName: ".*BAM_SPLIT_BY_REGION:SAMTOOLS_INDEX" {
        // The BAM_SPLIT_BY_REGION SWF only works with bais, so `params.fasta_largeref` should not be passed to it.
        tag = { "${meta.reference}:${meta.genomic_region}|${meta.sample_id}_${meta.library_id}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}_${meta.genomic_region}_dedupped" }
        publishDir = [
            enabled: false
        ]
    }

    withName: SAMTOOLS_SORT_DEDUPPED {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}_dedupped" }
        publishDir = [
            path: { "${params.outdir}/deduplication/" },
            mode: params.publish_dir_mode,
            pattern: '*.bam'
        ]
    }

    withName: SAMTOOLS_INDEX_DEDUPPED {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args = { params.fasta_largeref ? "-c" : "" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}_dedupped" }
        publishDir = [
            path: { "${params.outdir}/deduplication/" },
            mode: params.publish_dir_mode,
            pattern: '*.{bai,csi}'
        ]
    }

    withName: SAMTOOLS_FLAGSTAT_DEDUPPED {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}_dedupped" }
        publishDir = [
            path: { "${params.outdir}/deduplication/" },
            mode: params.publish_dir_mode,
            pattern: '*.flagstat'
        ]
    }

    //
    // HOST REMOVAL
    //
    withName: HOST_REMOVAL {
        tag = { "${meta.reference}|${meta_fastqs.sample_id}_${meta_fastqs.library_id}_L${meta_fastqs.lane}" }
        ext.args = {[
            "-m ${params.host_removal_mode}",
            "${meta_fastqs.single_end}" == false && "${params.preprocessing_skippairmerging}" == false ? "-merged" : ""
        ].join(' ').trim()}
        publishDir = [
            path: { "${params.outdir}/host_removal/" },
            mode: params.publish_dir_mode,
            pattern: '*.fq.gz'
            ]
    }

    //
    // PRESEQ
    //
    withName: PRESEQ_CCURVE {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args = "-B -s ${params.mapstats_preseq_stepsize}"
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/mapstats/preseq" },
            mode: params.publish_dir_mode
        ]
    }

    withName: PRESEQ_LCEXTRAP {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args   = [
            "-B",
            "-s ${params.mapstats_preseq_stepsize}",
            "-e ${params.mapstats_preseq_maxextrap}",
            "-x ${params.mapstats_preseq_terms}",
            "-n ${params.mapstats_preseq_bootstrap}",
            "-c ${params.mapstats_preseq_cval}",
            params.mapstats_preseq_defects_mode ? '-D' : ''
        ].join(' ').trim()
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/mapstats/preseq" },
            mode: params.publish_dir_mode
        ]
    }

    //
    //  BEDTOOLS_COVERAGE
    //
    withName: SAMTOOLS_VIEW_GENOME {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        publishDir = [
            enabled: false
        ]
    }

    withName: BEDTOOLS_COVERAGE_DEPTH {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args = '-mean -nonamecheck'
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}_depth" }
        publishDir = [
            path: { "${params.outdir}/mapstats/bedtools" },
            mode: params.publish_dir_mode
        ]
    }

    withName: BEDTOOLS_COVERAGE_BREADTH {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args = '-nonamecheck'
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}_breadth" }
        publishDir = [
            path: { "${params.outdir}/mapstats/bedtools" },
            mode: params.publish_dir_mode
        ]
    }


    //
    // DAMAGE MANIPULATION
    //

    withName: BEDTOOLS_MASKFASTA {
        ext.prefix = { "${meta.id}.masked" }
        publishDir = [
            path: { "${params.outdir}/reference/masked_reference/" },
            mode: params.publish_dir_mode,
            pattern: '*.masked.fa'
        ]
    }

    withName: MAPDAMAGE2 {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args = { [
            "--rescale",
            // "--rescale-out=${task.ext.prefix}.bam", // This doesn't work, because the output is expected to be in the mapdamage results dir.
            "--seq-length=${params.damage_manipulation_rescale_seqlength}",
            params.damage_manipulation_rescale_length_3p != 0 ? "--rescale-length-3p=${params.damage_manipulation_rescale_length_3p}" : "",
            params.damage_manipulation_rescale_length_5p != 0 ? "--rescale-length-5p=${params.damage_manipulation_rescale_length_5p}" : "",
            { meta.strandedness } == "single" ? '--single-stranded' : ''
        ].join(' ').trim() }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}_rescaled" }
        publishDir = [
            [
                path: { "${params.outdir}/damage_manipulation/" },
                mode: params.publish_dir_mode,
                pattern: 'result*/*.rescaled.bam',
                saveAs: { "${meta.sample_id}_${meta.library_id}_${meta.reference}_rescaled.bam" } // Needed to save the bam directly in the output dir instead of within the mapdamage results dir.
            ],
            [
                path: { "${params.outdir}/damage_manipulation/" },
                mode: params.publish_dir_mode,
                pattern: 'results_*/Stats_out_MCMC_*'
            ]
        ]
    }

    withName: SAMTOOLS_INDEX_DAMAGE_RESCALED {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args = { params.fasta_largeref ? "-c" : "" }
        publishDir = [
            path: { "${params.outdir}/damage_manipulation/" },
            mode: params.publish_dir_mode,
            pattern: '*.{bai,csi}',
            saveAs: { "${meta.sample_id}_${meta.library_id}_${meta.reference}_rescaled.bam.bai" } // ext.prefix cannot be used here, so rename for publishing instead.
        ]
    }

    withName: PMDTOOLS_FILTER {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args2 = {[
            "${meta.damage_treatment}" == 'none' ? '--UDGminus' : "${meta.damage_treatment}" == 'half' ? '--UDGhalf' : '--UDGplus' ,
            "${meta.strandedness}" == 'single' ? '--ss' : ''
        ].join(' ').trim()}
        ext.args3 = { "-h" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}_pmdfiltered" }
        publishDir = [
            path: { "${params.outdir}/damage_manipulation/" },
            mode: params.publish_dir_mode,
            pattern: '*_pmdfiltered.bam'
        ]
    }

    withName: SAMTOOLS_INDEX_DAMAGE_FILTERED {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args = { params.fasta_largeref ? "-c" : "" }
        publishDir = [
            path: { "${params.outdir}/damage_manipulation/" },
            mode: params.publish_dir_mode,
            pattern: '*.{bai,csi}'
        ]
    }

    withName: SAMTOOLS_FLAGSTAT_DAMAGE_FILTERED {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}_pmdfiltered" }
        publishDir = [
            path: { "${params.outdir}/damage_manipulation/" },
            mode: params.publish_dir_mode,
            pattern: '*.flagstat'
        ]
    }

    withName: BAMUTIL_TRIMBAM {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args = [
            params.damage_manipulation_bamutils_softclip ? '-c' : ''
        ].join(' ').trim()
        ext.prefix = { params.run_pmd_filtering ? "${meta.sample_id}_${meta.library_id}_${meta.reference}_pmdfiltered_trimmed" : "${meta.sample_id}_${meta.library_id}_${meta.reference}_trimmed" }
        publishDir = [
            path: { "${params.outdir}/damage_manipulation/" },
            mode: params.publish_dir_mode,
            pattern: '*_trimmed.bam'
        ]
    }

    withName: SAMTOOLS_INDEX_DAMAGE_TRIMMED {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args = { params.fasta_largeref ? "-c" : "" }
        publishDir = [
            path: { "${params.outdir}/damage_manipulation/" },
            mode: params.publish_dir_mode,
            pattern: '*.{bai,csi}'
            ]
    }

    //
    // CONTAMINATION ESTIMATION
    //
    withName: ANGSD_DOCOUNTS {
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}" }
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args = [
            "-iCounts 1",
            "-r ${params.contamination_estimation_angsd_chrom_name}:${params.contamination_estimation_angsd_range_from}-${params.contamination_estimation_angsd_range_to}",
            "-minMapQ ${params.contamination_estimation_angsd_mapq}",
            "-minQ ${params.contamination_estimation_angsd_minq}"
        ].join(' ').trim()
        publishDir = [
            enabled: false
        ]
    }

    withName: ANGSD_CONTAMINATION {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/contamination_estimation/angsd/"},
            mode: params.publish_dir_mode,
            pattern: '*.txt'
        ]
    }

    withName: PRINT_CONTAMINATION_ANGSD {
        publishDir = [
            path: { "${params.outdir}/contamination_estimation/angsd/"},
            mode: params.publish_dir_mode,
            pattern: 'nuclear_contamination*'
        ]
    }

    //
    // METAGENOMIC SCREENING
    //
    withName: PRINSEQPLUSPLUS {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args =  [
                params.metagenomics_prinseq_mode == 'dust' ? "-lc_dust=${params.metagenomics_prinseq_dustscore}" : "-lc_entropy=${params.metagenomics_complexity_entropy}",
                "-trim_qual_left=0 -trim_qual_left=0 -trim_qual_window=0 -trim_qual_step=0",
            ].join(' ').trim()
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_complexity" }
        publishDir = [
            [
                path: { "${params.outdir}/metagenomic_complexity_filter/" },
                mode: params.publish_dir_mode,
                pattern: '*{_good_out.fastq.gz,_good_out_R1.fastq.gz,_good_out_R2.fastq.gz,log}',
                enabled: params.metagenomics_complexity_savefastq
            ]
        ]
    }

    withName: ".*BBMAP_BBDUK" {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args = { "entropymask=f entropy=${params.metagenomics_complexity_entropy}" }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_complexity" }
        publishDir = [
            path: { "${params.outdir}/metagenomic_complexity_filter/" },
            mode: params.publish_dir_mode,
            pattern: '*.{fastq.gz,log}',
            enabled: params.metagenomics_complexity_savefastq
        ]
    }

    //
    // MT-NUCLEAR RATIO
    //
    withName: MTNUCRATIO {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        publishDir = [
            enabled: false
        ]
    }

    withName: 'QUALIMAP_BAMQC_WITHBED|QUALIMAP_BAMQC_NOBED' {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        publishDir = [
            path: { "${params.outdir}/mapstats/qualimap/${meta.reference}/${meta.sample_id}/}" },
            mode: params.publish_dir_mode,
            enabled: true
        ]
    }

    //
    // DAMAGE CALCULATION
    //
    withName: DAMAGEPROFILER {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args   = [
            "-l ${params.damagecalculation_damageprofiler_length}",
            "-t ${params.damagecalculation_xaxis}",
            "-yaxis_dp_max ${params.damagecalculation_yaxis}"
        ].join(' ').trim()
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/damage_estimation/damageprofiler/" },
            mode: params.publish_dir_mode,
            enabled: true
        ]
    }

    withName: CALCULATE_MAPDAMAGE2 {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.args = { [
            "--no-stats",
            "-y ${params.damagecalculation_yaxis}",
            params.damagecalculation_mapdamage_downsample != 0 ? "-n ${params.damagecalculation_mapdamage_downsample} --downsample-seed=1" : "",
            { meta.strandedness } == "single" ? '--single-stranded' : '',
            "-m ${params.damagecalculation_xaxis}"
        ].join(' ').trim() }
        ext.prefix = { "${meta.sample_id}_${meta.library_id}_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/damage_estimation/mapDamage2/" },
            mode: params.publish_dir_mode,
            pattern: 'results_*/*'
        ]
    }

    //
    // RUN SEXDETERRMINE
    //
    withName: SAMTOOLS_DEPTH_SEXDETERRMINE {
        tag = { "${meta1.reference}|${meta1.sample_id}_${meta1.library_id}" }
        ext.prefix = { "${meta2.id}_samtoolsdepth" }
        ext.args = '-aa -q30 -Q30 -H'
        publishDir = [
        enabled: false
        ]
    }

    withName: SEXDETERRMINE {
        tag = { "${meta.reference}|${meta.sample_id}_${meta.library_id}" }
        ext.prefix = { "${meta.reference}_sexdeterrmine" }
        publishDir = [
            path: { "${params.outdir}/sex_determination/" },
            mode: params.publish_dir_mode,
            pattern: '*{_sexdeterrmine}*',
            enabled: true
        ]
    }

    //
    // LIBRARY MERGE
    //

    withName: SAMTOOLS_MERGE_LIBRARIES {
        tag = { "${meta.reference}|${meta.sample_id}" }
        ext.prefix = { "${meta.sample_id}_${meta.reference}_unsorted" }
        publishDir = [
            enabled: false
        ]
    }

    withName: SAMTOOLS_SORT_MERGED_LIBRARIES {
        tag = { "${meta.reference}|${meta.sample_id}" }
        ext.prefix = { "${meta.sample_id}_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/final_bams/" },
            mode: params.publish_dir_mode,
            pattern: '*.bam'
        ]
    }

    withName: SAMTOOLS_INDEX_MERGED_LIBRARIES {
        tag = { "${meta.reference}|${meta.sample_id}" }
        ext.args = { params.fasta_largeref ? "-c" : "" }
        ext.prefix = { "${meta.sample_id}_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/final_bams/" },
            mode: params.publish_dir_mode,
            pattern: '*.{bai,csi}'
        ]
    }

    withName: SAMTOOLS_FLAGSTAT_MERGED_LIBRARIES {
        tag = { "${meta.reference}|${meta.sample_id}" }
        ext.prefix = { "${meta.sample_id}_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/final_bams/" },
            mode: params.publish_dir_mode,
            pattern: '*.flagstat'
        ]
    }

    //
    // GENOTYPING
    //

    withName: SAMTOOLS_MPILEUP_PILEUPCALLER {
        tag = { "${meta.reference}|${meta.strandedness}" }
        ext.args = [
            "-B",
            "-q ${params.genotyping_pileupcaller_min_base_quality}",
            "-Q ${params.genotyping_pileupcaller_min_map_quality}",
        ].join(' ').trim()
        ext.prefix = { "${meta.strandedness}_${meta.reference}" }
        publishDir = [
            enabled: false
        ]
    }

    withName: SEQUENCETOOLS_PILEUPCALLER {
        tag = { "${meta.reference}|${meta.strandedness}" }
        ext.args = {[
            "--${params.genotyping_pileupcaller_method}",
            params.genotyping_pileupcaller_transitions_mode == "SkipTransitions" ? "--skipTransitions" : params.genotyping_pileupcaller_transitions_mode == "TransitionsMissing" ? "--transitionsMissing" : "",
            "${meta.strandedness}" == 'single' ? "--singleStrandMode" : "" ,
            "--sampleNames", meta.sample_id.join(","),
            "-e pileupcaller.${meta.strandedness}.${meta.reference}"
        ].join(' ').trim() }
        ext.prefix = { "${meta.strandedness}_${meta.reference}" }
        publishDir = [
            enabled: false // Not published because the output goes through COLLECT_GENOTYPES
        ]
    }

    withName: COLLECT_GENOTYPES {
        tag = { "${meta.reference}" }
        ext.prefix = { "pileupcaller_genotypes_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/genotyping/" },
            mode: params.publish_dir_mode,
            enabled: true,
            pattern: '*.{geno,snp,ind}'
        ]
    }

    withName: EIGENSTRATDATABASETOOLS_EIGENSTRATSNPCOVERAGE {
        tag = { "${meta.reference}" }
        ext.args = { "-j ${prefix}.json" }
        ext.prefix = { "pileupcaller_genotypes_${meta.reference}_coverage" }
        publishDir = [
            path: { "${params.outdir}/genotyping/" },
            mode: params.publish_dir_mode,
            enabled: true,
            pattern: '*.{tsv}'
        ]
    }

    withName: GATK_REALIGNERTARGETCREATOR {
        tag = { "${meta.reference}|${meta.sample_id}" }
        ext.args = [
            params.genotyping_gatk_ug_defaultbasequalities > 0 ? "--defaultBaseQualities ${params.genotyping_gatk_ug_defaultbasequalities}" : "", // Empty string since GATK complains if its default of -1 is provided.
        ].join(' ').trim()
        ext.prefix = { "${meta.sample_id}_${meta.reference}_realigntarget" }
        publishDir = [
            enabled: false
        ]
    }

    withName: GATK_INDELREALIGNER {
        tag = { "${meta.reference}|${meta.sample_id}" }
        ext.args = [
            params.genotyping_gatk_ug_defaultbasequalities > 0 ? "--defaultBaseQualities ${params.genotyping_gatk_ug_defaultbasequalities}" : "", // Empty string since GATK complains if its default of -1 is provided.
        ].join(' ').trim()
        ext.prefix = { "${meta.sample_id}_${meta.reference}_realigned" }
        publishDir = [
            path: { "${params.outdir}/genotyping/IndelRealigner" },
            mode: params.publish_dir_mode,
            enabled: params.genotyping_gatk_ug_keeprealignbam,
            pattern: '*.{bam,bai}'
        ]
    }

    withName: GATK_UNIFIEDGENOTYPER {
        tag = { "${meta.reference}|${meta.sample_id}" }
        ext.args = {[
            "--sample_ploidy ${meta2.ploidy}",
            "-stand_call_conf ${params.genotyping_gatk_call_conf}",
            "-dcov ${params.genotyping_gatk_ug_downsample}",
            "--output_mode ${params.genotyping_gatk_ug_out_mode}",
            "--genotype_likelihoods_model ${params.genotyping_gatk_ug_genotype_mode}",
            params.genotyping_gatk_ug_defaultbasequalities > 0 ? "--defaultBaseQualities ${params.genotyping_gatk_ug_defaultbasequalities}" : "", // Empty string since GATK complains if its default of -1 is provided.
        ].join(' ').trim() }
        ext.prefix = { "${meta.sample_id}_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/genotyping/" },
            mode: params.publish_dir_mode,
            enabled: true,
            pattern: '*.vcf.gz'
        ]
    }

    withName: BCFTOOLS_INDEX_UG {
        tag = { "${meta.reference}|${meta.sample_id}" }
        ext.args = "--tbi" //tbi indices for consistency with GATK HC
        ext.prefix = { "${meta.sample_id}_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/genotyping/" },
            mode: params.publish_dir_mode,
            enabled: true,
            pattern: '*.vcf.gz.tbi'
        ]
    }

    withName: GATK4_HAPLOTYPECALLER {
        tag = { "${meta.reference}|${meta.sample_id}" }
        ext.args = {[
            // Option names have changed from underscore_separated to hyphen-separated in GATK4
            "--sample-ploidy ${meta2.ploidy}",
            "-stand-call-conf ${params.genotyping_gatk_call_conf}",
            "--output-mode ${params.genotyping_gatk_hc_out_mode}",
            "--emit-ref-confidence ${params.genotyping_gatk_hc_emitrefconf}",
        ].join(' ').trim() }
        ext.prefix = { "${meta.sample_id}_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/genotyping/" },
            mode: params.publish_dir_mode,
            enabled: true,
            pattern: '*.{vcf.gz,vcf.gz.tbi}'
        ]
    }

    withName: FREEBAYES {
        tag = { "${meta.reference}|${meta.sample_id}" }
        ext.args = {[
            "-p ${ref_meta.ploidy}",
            "-C ${params.genotyping_freebayes_min_alternate_count}",
            params.genotyping_freebayes_skip_coverage != 0 ? "-g ${params.genotyping_freebayes_skip_coverage}" : "",
        ].join(' ').trim() }
        ext.prefix = { "${meta.sample_id}_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/genotyping/" },
            mode: params.publish_dir_mode,
            enabled: true,
            pattern: '*.vcf.gz'
        ]
    }

    withName: BCFTOOLS_INDEX_FREEBAYES {
        tag = { "${meta.reference}|${meta.sample_id}" }
        ext.args = "--tbi" //tbi indices for consistency with GATK HC
        ext.prefix = { "${meta.sample_id}_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/genotyping/" },
            mode: params.publish_dir_mode,
            enabled: true,
            pattern: '*.vcf.gz.tbi'
        ]
    }

    withName: BCFTOOLS_STATS_GENOTYPING {
        tag = { "${meta.reference}|${meta.sample_id}" }
        ext.prefix = { "${meta.sample_id}_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/genotyping/" },
            mode: params.publish_dir_mode,
            enabled: true,
            pattern: '*.txt'
        ]
    }

    withName: ANGSD_GL {
        tag = { "${meta.reference}" }
        ext.args = {
            gl_model = params.genotyping_angsd_glmodel == 'samtools' ? 1 : params.genotyping_angsd_glmodel == 'gatk' ? 2 : params.genotyping_angsd_glmodel == 'soapsnp' ? 3 : 4
            gl_format = params.genotyping_angsd_glformat == 'binary' ? 1 : params.genotyping_angsd_glformat == 'beagle_binary' ? 2 : params.genotyping_angsd_glformat == 'binary_three' ? 3 : 4
            [
                ( gl_format == 2 || gl_format == 3 ) ? '-doMajorMinor 1': '',
                "-GL ${gl_model}",
                "-doGlf ${gl_format}",
            ].join(' ').trim()
        }
        ext.prefix = { "angsd_${meta.reference}" }
        publishDir = [
            path: { "${params.outdir}/genotyping/" },
            mode: params.publish_dir_mode,
            enabled: true,
            pattern: '*.{glf,beagle}.gz'
        ]
    }
}
